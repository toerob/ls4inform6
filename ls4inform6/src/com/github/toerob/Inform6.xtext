grammar com.github.toerob.Inform6 with org.eclipse.xtext.common.Terminals

generate inform6 "http://www.xtext.org/toerob/inform6/Inform6"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

/*
Inform 6 grammar for Xtext lexer and parser generation for use with the 
Language Server Protocol (LSP) as a code assister within various of editors

 
The MIT License (MIT)

Copyright (c) 2020 Tomas Öberg

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in
all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
THE SOFTWARE. 
 */
 
Program:
	(directives+=Directive | imports+=IncludeDeclaration)*;


//=============================================================
// D I R E C T I V E S 
//=============================================================
Directive:
	ReleaseDirective
	| ZcharacterDirective
	| CommentDeclaration
	| ReplaceDirective
	| SerialDirective
	| SwitchesDeclaration
	| GlobalDeclaration
	| GlobalDefaultDeclaration
	| GlobalConstantDeclaration
	| GlobalFunctionDefinition
	| AttributeDeclaration
	| PropertyDeclaration
	| ClassDeclaration
	| ObjectDeclaration
	| IdlessObjectDeclaration
	| NearbyDeclaration
	| VerbDeclaration
	| ArrayDeclaration
	| PropertyDirective
	| MessageDirective
	| {Directive} AbbreviateDirective
	| {Directive} AbortDirective
	| {Directive} EndDirective
	| {Directive} LowStringDirective
	| {Directive} PreprocessorDirective
	| {Directive} FakeActionDirective
	| {Directive} SystemFileDirective
;


SystemFileDirective:
	'System_file' ';'
;

FakeActionDirective:
	'Fake_Action' ID ';'
;

AbbreviateDirective:
	'Abbreviate' STRING ';'
;

AbortDirective:
	'abort' ';'
;

MessageDirective:
	'Message' name=ID? string=STRING ';' 
;

ZcharacterDirective: 
	'Zcharacter' string=STRING ';' 
;

CommentDeclaration:
	CompilerDirective
	| {CommentDeclaration} SL_COMMENT
;

CompilerDirective:
	value=COMPILER_DIRECTIVE_COMMENT
;


IncludeDeclaration:
	('Include'|'Link') importURI=STRING ';';

EndDirective:
	"End" ';'
;

//FIXME: DIRECTIONS should be able to cross reference the same way as attributes
PropertyDirective:
	"Property" additive='additive'? name=(ID|DIRECTIONS|'found_in') value=Primary? ';'
;

LowStringDirective:
	"Lowstring" ID STRING ";"
;

ReleaseDirective:	name="Release" version=INT ';';
ReplaceDirective:	name="Replace" id=ID ';';
SerialDirective:	name="Serial" serial=STRING ';';

GlobalConstantDeclaration:
	'Constant' name=ID '='? value=GlobalConstantValue? ';';

GlobalDefaultDeclaration:
	'Default' name=ID '='? value=GlobalConstantValue? ';';

SwitchesDeclaration:	name='Switches' (switches+=Primary*) ';';

//FIXME: 'meta' here should be a global declaration, and instead used as a [GlobalDeclaration]
// in the grammar instead of a keyword.
GlobalDeclaration:		'Global' name=(ID|'meta') ('=' value=Primary)? ';';
GlobalConstantValue:	name=Primary;

AttributeDeclaration: 	{AttributeDeclaration} 'Attribute' 
	(
		name=(ID|STATIC) ('alias' aliasedAttribute=(ID|STATIC))? 
	)
	';'
;


PropertyDeclaration: 	'Property' 
	(
		name=ID additive='additive'? value=Primary 
		| name=ID 'alias' aliasedProperty=Primary
	)  
	';'
;

ClassDeclaration:
	{ClassDeclaration} 
		('Class') name=ID 
			(cardinality='(' max=Primary ')')?
				(properties+=(ClassSection|PropertySection|AttributeSection))? 
				(','? properties+=(ClassSection|PropertySection|AttributeSection))*
		';'
	;	

ObjectDeclaration:
	({ObjectDeclaration} (object='Object'|superType=[ClassDeclaration|ID]) level+=BYTE_ARROW* name=ID headline=STRING? in=[ObjectDeclaration]?
		(properties+=(ClassSection|PropertySection|AttributeSection))? 
		(','? properties+=(ClassSection|PropertySection|AttributeSection))*)
	';'
;

IdlessObjectDeclaration:
	{ObjectDeclaration} (object='Object'|superType=[ClassDeclaration|ID]) level+=BYTE_ARROW* name=STRING? in=[ObjectDeclaration]?
		(properties+=(ClassSection|PropertySection|AttributeSection))? 
		(','? properties+=(ClassSection|PropertySection|AttributeSection))*
	';'
;

NearbyDeclaration:
	{ObjectDeclaration} 'Nearby' name=ID? headline=STRING in=[ObjectDeclaration|ID]?
		(properties+=(ClassSection|PropertySection|AttributeSection))? 
		(','? properties+=(ClassSection|PropertySection|AttributeSection))*
	';'
;

VerbDeclaration:
	{VerbDeclaration} name='Verb' ^meta='meta'? syn+=STRING* defintions+=VerbBody* ';' 
	| {VerbDeclaration} name='Extend' only='only'? syn+=STRING* ('first'|'last')? defintions+=VerbBody* ';' 
;

VerbBody:
	'*' definitions+=VerbAssignment* BYTE_ARROW name=ID reverse='reverse'?
;

VerbAssignment:
	Primary
	| ID '=' Primary
	| {VerbAssignment} STRING ('/' STRING)+
;

ArrayDeclaration:
	'Array' name=(ID|'buffer') STATIC? arrayBody=ArrayType
	';'
;

// TODO: See Swedish.h, arrays need to be further refined
ArrayType:
	{BYTE_ARRAY} type=BYTE_ARROW size=PrimaryExpression? content+=PrimaryExpression* 
	| {WORD_ARRAY} type=WORD_ARROW size=PrimaryExpression? content+=PrimaryExpression*
	| {TABLE_ARRAY} type=('table'|'buffer') size=PrimaryExpression? content+=PrimaryExpression*
	| {STRING_ARRAY} type='string' size=PrimaryExpression? content+=STRING?
;

// FIXME: Just enough to make statements such as
// PARSE_BUFFER_LEN/WORDSIZE pass. This needs more work 
// so it can handle nested expressions etc
PrimaryExpression returns Expression:
	left=Primary operator=('/'|'*'|'+'|'-') right=Primary
	| '(' left=Primary operator=('/'|'*'|'+'|'-') right=Primary ')'
	| '(' left=Primary ')'
	| left=Primary
	| {Expression} '(' unparsedTokens+=FuzzyExpression* ')' //Fuzzy parse if too complex
	//LogicalOr //TODO: This line shall replace all of the above in this rule when it is complete
;

/*
LogicalOr returns Expression:
	BitwiseOr ({LogicalOr.left=current} ('||' | 'or') right=BitwiseOr)*;
BitwiseOr returns Expression:
	LogicalAnd ({BitwiseOr.left=current} '|' right=LogicalAnd)*;
LogicalAnd returns Expression:
	BitwiseAnd ({LogicalAnd.left=current} ('&&' | 'and') right=BitwiseAnd)*;
BitwiseAnd returns Expression:
	Equivalency ({BitwiseAnd.left=current} '&' right=Equivalency)*;
Equivalency returns Expression:
	Relational ({Equivalency.left=current} ('==' | '~=' | '=') right=Relational)*;
Relational returns Expression:
	Addition ({Relational.left=current} ('>=' | '<=' | '>' | '<') right=Addition)*;
Addition returns Expression:
	Multiplicity ({Addition.left=current} ('+' | '-') right=Multiplicity)*;
Multiplicity returns Expression:
	Unary ({Multiplicity.left=current} ('/' | '*') right=Unary)*;
Unary returns Expression:
	Grouped | ({Unary} type=('~') expr=Grouped);
Grouped returns Expression:
	Primary
	//| '(' PrimaryExpression ')'  	
;*/

ClassSection:
	({PropertySection} name='class' (classProperty=Property))	
;

PropertySection:
	({PropertySection} name=('with'|'private') 
		(properties+=Property)? (',' properties+=Property)*
		| {PropertySection} PreprocessorDirective	
	) ','?
;

AttributeSection:
	{AttributeSection} name='has' attributes+=Attribute (attributes+=Attribute)*;

//FIXME: DIRECTIONS here should be able to cross reference the same way as attributes, 
// so it should be removed and be replaced with something either as [PropertyDirective|ID] or
// ID if not declared before

Property:
	{Property} name=ID (method=Method | array=AbstractArray) 
	| 
	{Property}
		(name=DIRECTIONS) 
			(objectReference=[ObjectDeclaration|ID]
			|value=Primary
			|method=Method
			|array=AbstractArray)
	| {Property} 'found_in' (
			objectReferences+=[ObjectDeclaration|ID]*
			|method=Method
			|value=Primary
		)
;

AbstractArray:
	{AbstractArray} (array+=Primary? (array+=Primary)*)
	;

Method returns AbstractProperty:
	'[' functionBody=FunctionBody ']'
;

GlobalFunctionDefinition:
	'[' 
		functionHeader=FunctionHeader
		functionBody=FunctionBody 
	']'
	';'
;

FunctionHeader:
	{FunctionHeader} variables+=ID* ';'
;

FunctionBody:
	(FuzzyExpression)*
;

/* Encapsulates all possibly combinations into one abstract function body 
 * TODO: this should be broken up into real expression and statements, so real 
 * analysis can be made
 */
FuzzyExpression:
	Expression
	|';'|':'|'('|')'|'/'|'-'|'--'|'+'|'~'|'='|'!'|'*'|','|'#'|'##'
	|'string'
	|'reverse'
	|'meta'
	|'buffer'
	|'table'
	|'has'
	|'hasnt'
	|'class'
	|'Object'
	|'last'
	|'first'
	|'only'
	|'additive'
	|'alias'
	|'found_in'
	|'default'
	|'remove'|'move'|'to'
	|'give'
	|'in'|'notin'
;

Expression:	
	(ANY_OTHER|STRING|INT|ID|ML_COMMENT|SL_COMMENT|WS|STATIC
		|PreprocessorDirective
		|BYTE_ARROW
		|WORD_ARROW
		|DIRECTIONS
		|HEX
		|BINARY
	) ';'?	
;

Primary:
	{Primary} id=ID 
	| number=Number 
	| {Primary} string=STRING 
	| {Primary} direction=DIRECTIONS
	| {Primary} hex=HEX
	| {Primary} binary=BINARY
	;
	


Number:
	{Number} negative='-'? value=INT
;
Attribute:
	{Attribute} '~'? (name=[AttributeDeclaration|ID]|name=[AttributeDeclaration|STATIC])
;

terminal BYTE_ARROW: '->';
terminal WORD_ARROW: '-->';
	
terminal DIRECTIONS:
	'e_to'|'w_to'|'n_to'|'s_to'|'u_to'|'d_to'|'se_to'|'sw_to'|'nw_to'|'ne_to'|'in_to'|'out_to'
;

@Override	
terminal INT returns ecore::EInt:
	('0'..'9')* ('.' ('0'..'9')+)?;
	
terminal HEX:
	'$'  ('0'..'9'|'a'..'f'|'A'..'F')*;

terminal BINARY:
	'$' '$' ('0'|'1')*;

terminal COMPILER_DIRECTIVE_COMMENT: '!' '%' !('\n'|'\r')* ('\r'? '\n')?;

@Override 
terminal SL_COMMENT: '!' !('\n'|'\r')* ('\r'? '\n')?;

@Override 
terminal STRING:
			'"' ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|'"') )* '"' |
			"'" ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|"'") )* "'"
		;

terminal ^STATIC:
	'static'
;


PreprocessorDirective:
	ANY_PREPROCESSORDIR
;

// For now, just skip preprocessor directives rows
// Catch all tokens with this, in case it gets mixed up with addresses.
// TODO: It won't mix very well intermingled with conditionally defined 
// properties as it is now.
terminal ANY_PREPROCESSORDIR :
	'#'ID UNTIL_AND_WITH_SEMICOLON
;

terminal fragment UNTIL_AND_WITH_SEMICOLON:	!(';')* ';';

/* 
terminal IFDEF: '#'? I F D E F ;
terminal IFNDEF: '#'? I F N D E F ;
terminal ENDIF: '#'? E N D I F ;
terminal IFTRUE: '#'? I F T R U E ;
terminal IFFALSE: '#'? I F F A L S E ;
terminal IFNOT: '#'? I F N O T ;
terminal DEFINE: '#?' D E F I N E ;
terminal IFV: '#'? I F V ('3'|'5'|'8') ;
terminal fragment A: 'A'|'a';
terminal fragment B: 'B'|'b';
terminal fragment C: 'C'|'c';
terminal fragment D: 'D'|'d';
terminal fragment E: 'E'|'e';
terminal fragment F: 'F'|'f';
terminal fragment G: 'G'|'g';
terminal fragment H: 'H'|'h';
terminal fragment I: 'I'|'i';
terminal fragment J: 'J'|'j';
terminal fragment K: 'K'|'k';
terminal fragment L: 'L'|'l';
terminal fragment M: 'M'|'m';
terminal fragment N: 'N'|'n';
terminal fragment O: 'O'|'o';
terminal fragment P: 'P'|'p';
terminal fragment Q: 'Q'|'q';
terminal fragment R: 'R'|'r';
terminal fragment S: 'S'|'s';
terminal fragment T: 'T'|'t';
terminal fragment U: 'U'|'u';
terminal fragment V: 'V'|'v';
terminal fragment W: 'W'|'w';
terminal fragment X: 'X'|'x';
terminal fragment Y: 'Y'|'y';
terminal fragment Z: 'Z'|'z';
*/